// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM8.hdl
/*
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.

   16-bit register(bit):
   If load is asserted, the register's value is set to in;
   Otherwise, the register maintains its current value:
   if (load(t)) out(t+1) = in(t), else out(t+1) = out(t)

 * 8-way demultiplexor:
 * [a, b, c, d, e, f, g, h] = [in, 0,  0,  0,  0,  0,  0,  0] if sel = 000
 *                            [0, in,  0,  0,  0,  0,  0,  0] if sel = 001
 *                            [0,  0, in,  0,  0,  0,  0,  0] if sel = 010
 *                            [0,  0,  0, in,  0,  0,  0,  0] if sel = 011
 *                            [0,  0,  0,  0, in,  0,  0,  0] if sel = 100
 *                            [0,  0,  0,  0,  0, in,  0,  0] if sel = 101
 *                            [0,  0,  0,  0,  0,  0, in,  0] if sel = 110
 *                            [0,  0,  0,  0,  0,  0,  0, in] if sel = 111

 * 8-way 16-bit multiplexor:
 * out = a if sel = 000
 *       b if sel = 001
 *       c if sel = 010
 *       d if sel = 011
 *       e if sel = 100
 *       f if sel = 101
 *       g if sel = 110
 *       h if sel = 111
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    //select the register to activate
    DMux8Way(in=load, sel=address, 
            a=l0, b=l1, c=l2, d=l3, e=l4, f=l5, g=l6, h=l7);

    //change the output value
    Register(in=in, load=l0, out=out0);
    Register(in=in, load=l1, out=out1);
    Register(in=in, load=l2, out=out2);
    Register(in=in, load=l3, out=out3);
    Register(in=in, load=l4, out=out4);
    Register(in=in, load=l5, out=out5);
    Register(in=in, load=l6, out=out6);
    Register(in=in, load=l7, out=out7);

    //use mux8way16 to choose the output to emit
    Mux8Way16(a=out0, b=out1, c=out2, d=out3, e=out4, f=out5, g=out6, h=out7, 
            sel=address, out=out);
}